buildscript {
	repositories {
		maven {url='https://maven.minecraftforge.net/' }
		mavenCentral()
		maven {url='https://repo.spongepowered.org/repository/maven-public/'}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.version
group = project.maven_group

repositories {
	maven {
		url = "https://repo.unascribed.com"
	}
	maven {
		name = "JitPack"
		url = "https://jitpack.io"
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/releases"
	}
	maven {
		url = 'https://maven.ssf.tf/'
	}
	maven {
		url = 'https://repo.spongepowered.org/repository/maven-public/'
	}

}

mixin {
	add sourceSets.main, "libel.refmap.json"
}

minecraft {
	mappings channel: 'snapshot', version: '20171003-1.12'

	runs {
		client {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'
			property 'fml.coreMods.load', 'com.unascribed.libel.MirageCoreMod'
		}
		server {
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'
			property 'fml.coreMods.load', 'com.unascribed.libel.MirageCoreMod'
		}
	}

}

dependencies {
	minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

	implementation "com.unascribed:ears-api:1.4.1"
	implementation "tf.ssf.sfort:fscript-shell:2"
	compile 'org.spongepowered:mixin:0.8.1-SNAPSHOT'
}

processResources {
	inputs.property "version", project.version
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

jar {
	manifest {
		attributes([
				"FMLCorePlugin": "com.unascribed.libel.MirageCoreMod",
				"FMLCorePluginContainsFMLMod": "false",
				"ForceLoadAsMod": "true",
				"TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
				"TweakOrder": "0",
				"Specification-Title": "libel",
				"Specification-Version": "1",
				"Implementation-Title": project.name,
				"Implementation-Version": "${version}",
				"MixinConfigs": "libel.refmap.json"
		])
	}

	doFirst {
		StringBuilder bldr = new StringBuilder();
		File dir = file('build/classes/java/main');
		dir.eachFileRecurse(groovy.io.FileType.FILES, {
			if (it.name.endsWith(".class")) {
				bldr.append(it.getAbsolutePath().substring(dir.getAbsolutePath().length()+1))
				bldr.append("\n")
			}
		})
		file('build/tmp/classes.txt').text = bldr.toString();
	}
	// FabRefMap.txt is was manually written using
	// ~/.gradle/caches/minecraft/de/oceanlabs/mcp/mcp_snapshot/20170804/1.12.2/srgs/mcp-srg.srg
	from "fabRefMap.txt"
	from "LICENSE"
	from "build/tmp/classes.txt"
}
